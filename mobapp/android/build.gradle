buildscript {
    ext.kotlin_version = '2.1.10'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.android.tools.build:gradle:8.5.2"
        classpath "com.google.gms:google-services:4.4.2"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    // يضيف namespace تلقائياً لو ناقص في موديولات قديمة
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    namespace project.group
                }
            }
        }
    }

    // قَسْر نسخ محددة لمنع التعارضات
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            // توحيد كوتلن على 2.0.20
            if (details.requested.group == "org.jetbrains.kotlin") {
                details.useVersion("2.1.10")
            }
            // إبقاء core/core-ktx أقل من 1.17 لتوافق AGP 8.5.x
            if (details.requested.group == "androidx.core" &&
                    (details.requested.name == "core-ktx" || details.requested.name == "core")) {
                details.useVersion("1.13.1")
                details.because("Keep core/core-ktx below 1.17.0 to stay compatible with AGP 8.5.x")
            }
        }
    }
}

rootProject.buildDir = '../build'

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
